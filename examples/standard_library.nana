# TODO: Does Unquote even need to be in the standard library?
# When `Quote` sees a call to `Unquote` it will evaluate `value`.
Def "Unquote"
  Macro [value]
    value

Def "Defmacro"
  Macro [name args body]
    Quote
      Def Unquote name
        Macro Unquote args Unquote body

Defmacro "Log" [form]
  log(form)

Defmacro "AssertEq" [left right]
  Quote
    Match Unquote right
      [Unquote left :equal
       _             Log ["Assertion failed. Left: " Unquote left
                          " does not equal right: " Unquote right]]

Defmacro "Func" [name args body]
  Quote
    Def Unquote name
      Fn Unquote args Unquote body

Defmacro "Defm" [name args matches]
  Quote
    Func Unquote name Unquote args
      Match Unquote args
        Unquote matches

Defmacro "If" [cond if else]
  Quote
    Match Unquote cond
      [:true  Unquote if
       _      Unquote else]

AssertEq :if   If :true  :if :else
AssertEq :else If :false :if :else
AssertEq :else If :other :if :else

Defmacro "Or" [left right]
  Quote
    If Unquote left
      :true
      If Unquote right
        :true
        :false 

AssertEq :true  Or :true  :true
AssertEq :true  Or :true  :false
AssertEq :true  Or :false :true
AssertEq :false Or :false :false
 
Defm or [left right]
  [[:true _] :true
   [_ :true] :true
   _         :false]

AssertEq :true  or(:true  :true)
AssertEq :true  or(:true  :false)
AssertEq :true  or(:false :true)
AssertEq :false or(:false :false)

Defmacro "And" [left right]
  Quote
    If Unquote left
      If Unquote right
        :true
        :false 
      :false

AssertEq :true  And :true  :true
AssertEq :false And :true  :false
AssertEq :false And :false :true
AssertEq :false And :false :false

Defm and [left right]
  [[:true :true] :true
    _            :false]

AssertEq :true  and(:true  :true)
AssertEq :false and(:true  :false)
AssertEq :false and(:false :true)
AssertEq :false and(:false :false)

Defm not [bool]
  [[:true]  :false
   [:false] :true]

AssertEq :false not(:true)
AssertEq :true  not(:false)
